# LEP_higgs.sin
# Simple complete physics example: Higgs search at LEP plus scalar extension via Higgs portal

model = SM_Plus_Scalars_UFO (ufo)

alias n = ve:vm:vt
alias N = "ve~":"vm~":"vt~"
alias q = u:d:s:c
alias Q = "u~":"d~":"s~":"c~"
alias S = S1:S2:S3:S4

# The Higgsstrahlung process
process zh = "e-", "e+" => Z, H
process qqbb = "e-", "e+" => q, Q, b, "b~"
process qqbbwohiggs = "e-", "e+" => q, Q, b, "b~" { $restrictions = "!H" }
process qqxx = "e-", "e+" => q, Q, S, S
process qqnunu = "e-", "e+" => q, Q, n, N

compile

sqrts = 250 GeV

om = 5
MSk = 45 GeV
MH = 125.10 GeV
WH = 3.835 MeV
MB = 4.3 GeV

cuts = all M >= 10 GeV [q,Q]

$integration_method = "vamp2"
$rng_method = "rng_stream"
seed = 21092020

relative_error_goal = 0.01
accuracy_goal = 1

integrate (zh) { iterations = 5:5000}

!! integrate(qqbb, qqbbwohiggs)

# Define title etc. as global variables, that will be used by PLOT
$description =
  "A WHIZARD Tutorial Example. Light Higgs search at LEP. A 125 GeV SM-Higgs and a heavy scalar with only-Higgs couplings has been added. Luminosity enlarged by two orders of magnitude."
$y_label = "$N_{\textrm{events}}$"

# This is more than 100 times the LEP luminosity at 209 GeV.
luminosity = 0.660

# Allocate plots

$title = "$bb$ invariant mass distribution in $e^+e^- \to q \bar q X \bar X$"
$x_label = "$M_{b\bar b}$/GeV"
histogram m_bb (70 GeV, 130 GeV, 0.5 GeV)

$title = "Invariant recoil mass distribution in $e^+e^- \to q \bar q X \bar X$"
$x_label = "$M_{\textrm{rec}}$/GeV"
histogram m_rec (70 GeV, 130 GeV, 0.5 GeV)

$title = "Dijet invariant mass distribution in $e^+e^- \to q \bar q X \bar X$"
$x_label = "$M_{q\bar q}$/GeV"
histogram m_jj (70 GeV, 130 GeV, 0.5 GeV)

analysis = record m_bb (eval M [b,"b~"]);
               record m_rec (eval sqrt(sqrts^2 - 2 * sqrts * E + M2) / 1 GeV [combine [q,Q]]);
               record m_jj (eval M / 1 GeV [combine [q, Q]])
 
simulate (qqxx, qqbb, qqnunu) 

compile_analysis { $out_file = "lep_higgs.dat" }

