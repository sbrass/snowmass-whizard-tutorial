# Simple complete physics example: W endpoint in the Standard Model
#   e+ e- => μ anti-ν q Q
# Manual:
#   - 5.6.1 Initial state polarization
model = SM

alias q = u:c
alias Q = D:S

process eemunumqq = e1, E1 => e2, N2, q, Q

compile

!! Set muon mass to zero.
!! Set mass of strange and charm quark to zero for phase-space alias.
mmu = 0
mc = 0
ms = 0

!! Avoid divergencies and enhance convergence of the integration.
cuts = all M > 10 GeV [q, Q]
         and all E > 5 GeV [q]
         and all E > 5 GeV [Q]
         and all E > 5 GeV [e2]

sqrts = 500 GeV
beams = e1, E1

integrate (eemunumqq)

!! n_events = 10000
!! Use 1 / 10th of luminosity
luminosity = 500 / 10 / 1 fbarn !! default unit is fb⁻¹
$sample = "eenunumqq_500GeV_nopol"
sample_format = stdhep, lcio
simulate (eemunumqq)

beams_pol_density = @(-1), @(+1)
beams_pol_fraction = 80%, 30%

integrate (eemunumqq)

!! n_events = 10000
!! Use 1 / 10th of luminosity.
luminosity = 500 / 10 / 1 fbarn !! default unit is fb⁻¹
$sample = "eenunumqq_500GeV_pol"
sample_format = stdhep, lcio
simulate (eemunumqq)

show(results)
